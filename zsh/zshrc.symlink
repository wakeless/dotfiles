
# shortcut to this dotfiles path is $ZSH
export ZSH=$HOME/.dotfiles

export PATH="./bin:~/.local/bin:/usr/local/sbin:$ZSH/bin:$PATH"
export MANPATH="/usr/local/man:/usr/local/mysql/man:/usr/local/git/man:$MANPATH"

# your project folder that we can `c [tab]` to
export PROJECTS=~/Development

## Stash your environment variables in ~/.localrc. This means they'll stay out
## of your main dotfiles repository (which may be public, like this one), but
## you'll have access to them in your scripts.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi
#
## all of our zsh files
typeset -U config_files
config_files=($ZSH/**/*.zsh)

#
## load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

#
# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done
#
## initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit
#
# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files

# Better history
# Credits to https://coderwall.com/p/jpj_6q/zsh-better-history-searching-with-arrow-keys
autoload -U up-line-or-beginning-search
autoload -U down-line-or-beginning-search
zle -N up-line-or-beginning-search
zle -N down-line-or-beginning-search
bindkey "^[[A" up-line-or-beginning-search # Up
bindkey "^[[B" down-line-or-beginning-search # Down

source '/usr/local/Caskroom/google-cloud-sdk/latest/google-cloud-sdk/path.zsh.inc'
source '/usr/local/Caskroom/google-cloud-sdk/latest/google-cloud-sdk/completion.zsh.inc'

export PATH="$HOME/.yarn/bin:$HOME/.config/yarn/global/node_modules/.bin:$PATH"

